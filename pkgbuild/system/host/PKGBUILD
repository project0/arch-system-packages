# Maintainer: Richard Hillmann <richie at project0 dot de>
# inspired by https://github.com/Foxboron/PKGBUILDS/tree/master/foxboron-system
source ../base/lib.helper

pkgver="$(_pkgver)"
pkgrel="${PKGREL-1}"
pkgbase='project0-host'
pkgname=(
        "${pkgbase}-daedalus"
        "${pkgbase}-finalizer"
        "${pkgbase}-stargazer"
)
pkgdesc='Project0 package per host'
arch=('any')
license=('MIT')

# Lenovo T14 Gen 3 AMD
# package_project0-host-daedalus() {
#     depends=(
#         'project0-desktop-gnome' 'project0-refind-secureboot' 'project0-cryptsetup'
#         'vulkan-radeon'
#     )
#     printf 'daedalus' | install -Dm0644 /dev/stdin "${pkgdir}/etc/hostname"
#
#     # workaround amd gpu bug: https://gitlab.freedesktop.org/drm/amd/-/issues/1974
#     printf 'amdgpu.ppfeaturemask=0xfffd3fff' | install -Dm0644 /dev/stdin "${pkgdir}/etc/kernel/cmdline.preset.d/default/50-project0-amdgpu"
# }


# Flowers - Lenovo T14 Gen 3 Intel
package_project0-host-daedalus() {
    depends=(
        'project0-desktop-gnome' 'project0-refind-secureboot' 'project0-cryptsetup'
        # intel driver
        'intel-media-driver' 'vulkan-intel' 'libva-intel-driver'
    )
    printf 'daedalus' | install -Dm0644 /dev/stdin "${pkgdir}/etc/hostname"
}


# Intel NUC
package_project0-host-finalizer() {
    depends=(
        'project0-desktop-gnome' 'project0-refind'
    )
    printf 'finalizer' | install -Dm0644 /dev/stdin "${pkgdir}/etc/hostname"
}

# Homeserver - VM Host
package_project0-host-stargazer() {
    local address="192.168.200.240/24"
    local gw="192.168.200.1"
    local ipv6suffix="::240"

    depends=(
        'project0-base-config' 'project0-refind'
        'base-devel' 'yay-bin'

        # intel driver
        'intel-media-driver' 'vulkan-intel' 'libva-intel-driver'

        'incus' 'incus-tools'
    )
    printf 'stargazer' | install -Dm0644 /dev/stdin "${pkgdir}/etc/hostname"

    # lxd/incus
    _systemd_state incus.service enable

    # ssh
    printf "PermitRootLogin yes\n" \
        | install -Dm0644 /dev/stdin "${pkgdir}/etc/ssh/sshd_config.d/20-allow-root.conf"
    _systemd_state sshd.service enable

    # bridged network
    printf "[NetDev]\nName=br0\nKind=bridge\n\nMACAddress=none\n" \
        | install -Dm0644 /dev/stdin "${pkgdir}/etc/systemd/network/20-br0.netdev"
    printf "[Match]\nOriginalName=br0\n[Link]\nMACAddressPolicy=none\n" \
        | install -Dm0644 /dev/stdin "${pkgdir}/etc/systemd/network/20-br0.link"
    printf "[Match]\nName=br0\n[Network]\nAddress=%s\nGateway=%s\nConfigureWithoutCarrier=yes\nIPv6Token=%s\n" \
        "${address}" "${gw}" "${ipv6suffix}" \
        | install -Dm0644 /dev/stdin "${pkgdir}/etc/systemd/network/20-br0.network"
    # add phys. ethernet to bridge
    printf "[Match]\nName=en*\n[Network]\nBridge=br0\n" | install -Dm0644 /dev/stdin "${pkgdir}/etc/systemd/network/20-en.network"
}