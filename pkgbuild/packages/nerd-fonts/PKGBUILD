# Maintainer: Richard Hillmann <richie at project0 dot de>
# https://wiki.archlinux.org/title/Font_package_guidelines
pkgname=project0-nerd-fonts
pkgver=2.3.3
pkgrel=1
pkgdesc="Collection of patched nerd fonts"
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
license=('MIT')
conflicts=(
    nerd-fonts-source-code-pro
    nerd-fonts-fira-code
    nerd-fonts-droid-sans-mono
    nerd-fonts-hack
    nerd-fonts-jetbrains-mono
)

source=(
    "${pkgname}-${pkgver}-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE"
)

for s in SourceCodePro FiraCode DroidSansMono JetBrainsMono; do
    source+=("${pkgname}-${pkgver}-${s}.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v$pkgver/${s}.zip")
done

sha256sums=('245b522abb5aba0d8e84898bf6d5e95102620637626106bfd0061189a2370360'
            'f9c5cb60205f2945184f69e42460dd1289aa9d8effccc890c474307d1f9233bd'
            '63410350230ad9c1873df4b1e154066d809cff2490e5bbc440f05d7967ee8e2f'
            '14e744ab94048937c1121556ba5af42b30b08560a383d9af9ec4d19c34b457d4'
            '59dd2dc001a54e6d1c6c233449c51094650c0ef1fe76c87a8524eb5def4f1db8')

prepare() {
    find "$srcdir" -type f -iname '*windows*' -delete  # the windows compatible fonts are not necessary on linux ;)
}

package() {
    install -d ${pkgdir}/usr/share/fonts/TTF
    install -d ${pkgdir}/usr/share/licenses/${pkgname}
    install -Dm644 ${srcdir}/*.ttf ${pkgdir}/usr/share/fonts/TTF
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}-LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}
