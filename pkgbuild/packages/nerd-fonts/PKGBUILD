# Maintainer: Richard Hillmann <richie at project0 dot de>
# https://wiki.archlinux.org/title/Font_package_guidelines
pkgname=project0-nerd-fonts
pkgver=2.3.3
pkgrel=1
pkgdesc="Collection of patched nerd fonts"
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
license=('MIT')
conflicts=(
    nerd-fonts-source-code-pro
    nerd-fonts-fira-code
    nerd-fonts-droid-sans-mono
    nerd-fonts-hack
    nerd-fonts-jetbrains-mono
)

source=(
    "${pkgname}-${pkgver}-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE"
)

for s in SourceCodePro FiraCode DroidSansMono JetBrainsMono; do
    source+=("${pkgname}-${pkgver}-${s}.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v$pkgver/${s}.zip")
done

sha256sums=('1f6ad4edae6479aaace3112ede5279a23284ae54b2a34db66357aef5f64df160'
            '5fe3f414fc1c985c59283565176a71243f9983fb7ba5c9da37357d4b552eda15'
            '12c58f67731354f78e1a55030954bd326e380d1726c7f96f3db1089d53ecf609'
            '316e04b26f1eb2035a39981e02873ee2bacacbabfa176c1ee5e7a6edb3a04cd0'
            '187c316bb77ed1c824099fe8621a17d14f7de8a6ce5b14a1a04de0ae674b4aa6')

prepare() {
    find "$srcdir" -type f -iname '*windows*' -delete  # the windows compatible fonts are not necessary on linux ;)
}

package() {
    install -d ${pkgdir}/usr/share/fonts/TTF
    install -d ${pkgdir}/usr/share/licenses/${pkgname}
    install -Dm644 ${srcdir}/*.ttf ${pkgdir}/usr/share/fonts/TTF
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}-LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}
