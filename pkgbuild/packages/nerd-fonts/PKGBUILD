# Maintainer: Richard Hillmann <richie at project0 dot de>
# https://wiki.archlinux.org/title/Font_package_guidelines
pkgname=project0-nerd-fonts
pkgver=2.1.1
pkgrel=1
pkgdesc="Collection of patched nerd fonts"
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
license=('MIT')
conflicts=(
    nerd-fonts-source-code-pro
    nerd-fonts-fira-code
    nerd-fonts-droid-sans-mono
    nerd-fonts-hack
    nerd-fonts-jetbrains-mono
)

source=(
    "${pkgname}-${pkgver}-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/$pkgver/LICENSE"
)

for s in SourceCodePro FiraCode DroidSansMono JetBrainsMono; do
    source+=("${pkgname}-${pkgver}-${s}.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v$pkgver/${s}.zip")
done

sha256sums=('d2a29823384e9194a87936ccad495c764c2ef733b29bfa6f72a1d65803ce02e5'
            'a771689e0bc1d020e2082c705e2fb6113b7f8fbc1c56c639957f12546bd39619'
            '21de9aa0edaa3fd2dc1d87fb9ecec0b67c9b3b18bd1998a19904158067fea7e7'
            'ffdcbbed0f95d2f4030fdc35297ed94f8e1da4e9313cd510cd900b452c5beca0'
            '842013fa44b6896d4eb91635a81ef75244d78d7f61ff866c9dfd3315a67788cd')

prepare() {
    find "$srcdir" -type f -iname '*windows*' -delete  # the windows compatible fonts are not necessary on linux ;)
}

package() {
    install -d ${pkgdir}/usr/share/fonts/TTF
    install -d ${pkgdir}/usr/share/licenses/${pkgname}
    install -Dm644 ${srcdir}/*.ttf ${pkgdir}/usr/share/fonts/TTF
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}-LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}
