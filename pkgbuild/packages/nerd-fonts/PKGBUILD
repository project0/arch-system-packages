# Maintainer: Richard Hillmann <richie at project0 dot de>
# https://wiki.archlinux.org/title/Font_package_guidelines
pkgbase='project0-nerd-fonts'
pkgname=(
    project0-nerd-fonts
)

pkgver=3.0.2
pkgrel=1
pkgdesc="Collection of patched nerd fonts"
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
license=('MIT')
conflicts=(
    nerd-fonts-source-code-pro
    nerd-fonts-fira-code
    nerd-fonts-droid-sans-mono
    nerd-fonts-hack
    nerd-fonts-jetbrains-mono
)

depends=()
source=()

for s in SourceCodePro FiraCode DroidSansMono JetBrainsMono; do
    source+=("${pkgbase}-${pkgver}-${s}.tar.xz::https://github.com/ryanoasis/nerd-fonts/releases/download/v$pkgver/${s}.tar.xz")
    font=${s}
    depends_all+=("${pkgbase}-${s,,}")
    pkgname+=("${pkgbase}-${s,,}")

    # workaround license naming issue
    if [[ "$font" == "SourceCodePro" ]]; then
        font="SauceCodePro"
    fi
    # why does this font have a different name?
    if [[ "$font" == "DroidSansMono" ]]; then
        font="DroidSans"
    fi
    eval "package_project0-nerd-fonts-${s,,}() { _package \"${font}\"; }"
done

# meta package
package_project0-nerd-fonts() {
    depends=("${depends_all[@]}")
    install -d "${pkgdir}/usr/share/licenses/${pkgbase}"
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgbase}"
}

prepare() {
    find "$srcdir" -type f -iname '*windows*' -delete  # the windows compatible fonts are not necessary on linux ;)
}

_package() {
    font="$1"
    install -d "${pkgdir}/usr/share/fonts/TTF"
    install -Dm644 "${srcdir}/${font}"* "${pkgdir}/usr/share/fonts/TTF"
}

sha256sums=('e2ce1b45d071b2e549ce72d19a2f4e120b6b79f939fa954a40288917a3e2f863'
            '76c1d691cea44b0cae4d6add56bb3ef52b83cedebb1c5f519b62d068f8586b93'
            '8a5986ecc93b3835063bd05cf0e8af55e1769f50b002bb2cd70538fee8d3218f'
            '4991258b7c97071238a7459f0d3bf81a893ae7b0c849dbc47ad52833a8db7f55')
