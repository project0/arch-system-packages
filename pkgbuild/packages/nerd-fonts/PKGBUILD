# Maintainer: Richard Hillmann <richie at project0 dot de>
# https://wiki.archlinux.org/title/Font_package_guidelines
pkgname=project0-nerd-fonts
pkgver=2.2.2
pkgrel=1
pkgdesc="Collection of patched nerd fonts"
arch=('any')
url='https://github.com/ryanoasis/nerd-fonts'
license=('MIT')
conflicts=(
    nerd-fonts-source-code-pro
    nerd-fonts-fira-code
    nerd-fonts-droid-sans-mono
    nerd-fonts-hack
    nerd-fonts-jetbrains-mono
)

source=(
    "${pkgname}-${pkgver}-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE"
)

for s in SourceCodePro FiraCode DroidSansMono JetBrainsMono; do
    source+=("${pkgname}-${pkgver}-${s}.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v$pkgver/${s}.zip")
done

sha256sums=('245b522abb5aba0d8e84898bf6d5e95102620637626106bfd0061189a2370360'
            'a736448f04c73bdefefc356c024a5d9250ec086a0621c84179fc44587650c731'
            '20182e6e7c42cf8ab479d83af3200266261ec9bd4e80cdaceb793ecd56c9a398'
            'b6fef0c6f789d2f317b192588b5d7aab45ab7b237bd163deb4ed18269ad62192'
            '8b9b6c58081d179ecd50839a6b211dbd24b61e66d87715860129b6138982ee7b')

prepare() {
    find "$srcdir" -type f -iname '*windows*' -delete  # the windows compatible fonts are not necessary on linux ;)
}

package() {
    install -d ${pkgdir}/usr/share/fonts/TTF
    install -d ${pkgdir}/usr/share/licenses/${pkgname}
    install -Dm644 ${srcdir}/*.ttf ${pkgdir}/usr/share/fonts/TTF
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}-LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}
